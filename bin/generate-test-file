#!/usr/bin/env ys-0

defn main(slug):
  data =:
    json/load:
      slurp: ".problem/exercises/$slug/canonical-data.json"

  tests =:
    loop [[case & cases] data.cases, tests []]:
      if cases.seq():
        recur cases: tests.conj(gen-test(case))
        =>: tests

  test =: |
    #!/usr/bin/env ys-0

    require ys::taptest: :all

    use: $slug
    $gen-comments(data)
    test::
    $(join("\n" tests).trimr())

    done: # $count(tests)

  say: test

defn gen-test(case):
  test =:
    vector:
      apply merge::
      - name:: case.description.capitalize()
        code:: gen-code(case)
      - !
        if case.expected.error: +{'what' 'error'} {}
      - want:: case.expected.error || case.expected
        uuid:: case.uuid
        SKIP: true

  yaml/dump: test

defn gen-code(case):
  func =: case.property.kebab-symbol()
  args =: join(' ' case.input.vals().mapv(pr-str))
  =>: "${func}($args)"

defn gen-comments(data):
  if data.comments:
    then:
      comments =:
        data
          .comments
          .map(trim)
          .join("\n")
          .replace(/(?m)^/ '# ')
      =>: "\n$comments\n"
    else: ''

defn kebab-symbol(v):
  replace v /[A-Z]/:
    fn [c]: +"-" + c.lower-case()
